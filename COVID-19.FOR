C234567*****************************************************************
C      FORTRAN VERSION OF COVID-19 MODEL

	IMPLICIT DOUBLE PRECISION (A-H, O-Z)

C	D = DEATHS
C	FF = FATALITY FRACTION
C	YI = INFECTED
C	PE = PRECAUTIONS EFFECTIVENESS
C	R = RECOVERED
C	S = SUSCEPTIBLE

	REAL*8 D(2), FF(2), YI(2), PE(2), R(2), S(2)
	
	OPEN(3,FILE="INITIALS.TXT")
	OPEN(4,FILE="PARAMETERS.TXT")
	OPEN(7,FILE="OUTPUT.TXT")

C***********************************************************************
C	CONTROL PARAMETERS

	SIMTIME = 0.0
	ENDTIME  = 365.0
	XINC = 0.125
	SAVEPER = 1.0
	PTIME = 0.0

C***********************************************************************
C	READ MODEL PARAMETERS
	READ(4,*)

C	POP = POPULATION
	READ(4,*)
	READ(4,*)
	READ(4,*) POP

C	HERD = FRACTION REQUIRED FOR HERD IMMUNITY
	READ(4,*)
	READ(4,*)
	READ(4,*) HERD

C	UNCONF = UNCONFIRMED RATIO
	READ(4,*)
	READ(4,*)
	READ(4,*) UNCONF

C	RPST = REGULATED PRECAUTIONS START TIME
	READ(4,*)
	READ(4,*)
	READ(4,*) RPST

C	ZLTFF = LONG TERM FATALITY FRACTION
	READ(4,*)
	READ(4,*)
	READ(4,*) ZLTFF

C	TTCFF = TIME TO CHANGE FATALITY FRACTION
	READ(4,*)
	READ(4,*)
	READ(4,*) TTCFF

C	URR = UNCONTROLLED REPRODUCTION RATE
	READ(4,*)
	READ(4,*)
	READ(4,*) URR

C	ZID = ILLNESS DURATION
	READ(4,*)
	READ(4,*)
	READ(4,*) ZID

C	RD = REGULATIONS DURATION
	READ(4,*)
	READ(4,*)
	READ(4,*) RD

C	RPE = REGULATED PRECAUTIONS EFFECTIVENESS
	READ(4,*)
	READ(4,*)
	READ(4,*) RPE

C	TTIRPC = TIME TO IMPLEMENT REGULATED PRECAUTIONS
	READ(4,*)
	READ(4,*)
	READ(4,*) TTIRP

C	TTIVPC = TIME TO IMPLEMENT VOLUNTARY PRECAUTIONS
	READ(4,*)
	READ(4,*)
	READ(4,*) TTIVP

C	VPE = VOLUNTARY PRECAUTIONS EFFECTIVENESS
	READ(4,*)
	READ(4,*)
	READ(4,*) VPE

C	DAILY UNCONTROLLED REPRODUCTION RATE
	DURR = URR/ZID

C***********************************************************************
C	INITIAL VALUES
	READ(3,*)

C	XD = INITIAL DEATHS
	READ(3,*)
	READ(3,*)
	READ(3,*) XD

C	XFF = INITIAL FATALITY FRACTION
	READ(3,*)
	READ(3,*)
	READ(3,*) XFF

C	XI = INITIAL INFECTED
	READ(3,*)
	READ(3,*)
	READ(3,*) XI

C	XPE = INITIAL PRECAUTION EFFECTIVENESS
	READ(3,*)
	READ(3,*)
	READ(3,*) XPE

C	XR = INITIAL RECOVERED
	READ(3,*)
	READ(3,*)
	READ(3,*) XR

C	XS = INITIAL SUSCEPTIBLE
C	POP = POPULATION
C	HERD = FRACTION OF POPULATION TO OBTAIN HERD IMMUNITY
C	UNCONF = UNCONFIRMED CASES MULTIPLIER

	XS = POP * HERD / UNCONF

C***********************************************************************
C	INITIALIZE STOCKS

	D(1) = XD
	FF(1) = XFF
	YI(1) = XI
	PE(1) = XPE
	R(1) = XR
	S(1) = XS	

C***********************************************************************
C	START SIMULATION

5	IF (SIMTIME .GT. ENDTIME) GOTO 10

C	CC = CONFIRMED CASES
C	D = DEATHS
C	YI = INFECTED
C	R = RECOVERED

	CC = D(1) + YI(1) + R(1)

C***********************************************************************
C	PRINT LINE OF OUTPUT

	IF (ABS(SIMTIME - INT(PTIME)) .LT. 0.001) THEN
		WRITE(7,1000) SIMTIME, D(1), CC
1000		FORMAT(3f10.0)
		PTIME = PTIME + SAVEPER
	ENDIF

C***********************************************************************
C	CALCULATE AUXILIARIES

C	CIFF = CHANGE IN FATALITY FRACTION
C	ZLTFF = LONG TERM FATALITY FRACTION
C	FF = FATALITY FRACTION
C	TTCFF = TIME TO CHANGE FATALITY FRACTION

	IF (SIMTIME .LE. RPST) THEN
		CIFF = 0.0
	ELSE
		CIFF = (ZLTFF - FF(1)) / TTCFF
	ENDIF

C	RR = REPRODUCTION RATE
C	DURR = DAILY UNCONTROLLED REPRODUCTION RATE
C	PE = PRECAUTIONS EFFECTIVENESS

	RR = DURR * (1.0 - PE(1))

C	CRR = CONTROLLED REPRODUCTION RATE
C	RR = REPRODUCTION RATE
C	ZID = ILLNESS DURATION
	
	CRR = RR * ZID

C	DPD = DEATHS PER DAY
C	YI = INFECTED
C	FF = FATALITY FRACTION
C	ZID = ILLNESS DURATION

	DPD = YI(1) * FF(1) / ZID

C	YIPD = INFECTED PER DAY
C	S = SUSCEPTIBLE
C	XS = INITIAL SUSCEPTIBLE
C	YI = INFECTED
C	RR = REPRODUCTION RATE
 
	YIPD = (S(1) / XS) * YI(1) * RR

C	CIPE = CHANGE IN PRECAUTIONS EFFECTIVENESS
C	RPST = REGULATIONS START TIME
C	RD = REGULATIONS DURATION
C	RPE = REGULATIONS PRECAUTIONS EFFECTIVENESS
C	PE = PRECAUTIONS EFFECTIVENESS
C	TTIRP = TIME TO IMPLEMENT REGULATED PRECAUTIONS
C	VPE = VOLUNTARY PRECAUTIONS EFFECTIVENESS
C	TTIVP = TIME TO IMPLEMENT VOLUNTARY PRECAUTIONS

	IF (SIMTIME .LT. RPST) THEN
		CIPE = 0.0
	ELSEIF (SIMTIME .LE. (RPST + RD)) THEN
		CIPE = (RPE - PE(1)) / TTIRP
	ELSE
		CIPE = (VPE - PE(1)) / TTIVP
	ENDIF

C	RPD = RECOVERED PER DAY
C	YI = INFECTED
C	FF = FATALITY FRACTION
C	ZID = ILLNESS DURATION

	RPD = YI(1) * (1-FF(1)) / ZID

C***********************************************************************
C	UPDATE STOCK VARIABLES

C	D = DEATHS
C	DPD = DEATHS PER DAY

	D(2) = D(1) + XINC * DPD

C	FF = FATALITY FRACTION
C	CIFF = CHANGE IN FATALITY FRACTION

	FF(2) = FF(1) + XINC * CIFF

C	YI = INFECTED
C	YIPD = INFECTED PER DAY
C	DPD = DEATHS PER DAY
C	RPD = RECOVERED PER DAY

	YI(2) = YI(1) + XINC * (YIPD - DPD - RPD)

C	PE = PRECAUTIONS EFFECTIVENESS
C	CIPE = CHANGE IN PRECAUTIONS EFFECTIVENESS

	PE(2) = PE(1) + XINC * CIPE

C	R = RECOVERED
C	RPD = RECOVERED PER DAY

	R(2) = R(1) + XINC * RPD

C	S = SUSCEPTIBLE
C	YIPD = INFECTED PER DAY

	S(2) = S(1) + XINC * (-YIPD)

C***********************************************************************
C	INCREMENT SIMULATION TIME

	SIMTIME = SIMTIME + XINC

C***********************************************************************
C	RESET STOCK VARIABLES

	D(1) = D(2)
	FF(1) = FF(2)
	YI(1) = YI(2)
	PE(1) = PE(2)
	R(1) = R(2)
	S(1) = S(2)
	
	GOTO 5

C***********************************************************************
C	END OF SIMULATION

10	CONTINUE

	PRINT*, "CONFIRMED DEATHS ", D(2)
	PRINT*, "CONFIRMED CASES ", CC

	STOP
	END